apply plugin: 'com.android.tools.java-library'
apply plugin: 'com.android.tools.kotlin'
apply plugin: 'com.android.tools.publish'
apply plugin: 'com.android.tools.metalava'

configurations {
    compileOnlyForJavadoc {
        extendsFrom(configurations.compileOnly)
    }
}

dependencies {
    implementation libs.com.android.tools.common
    implementation libs.tomlj
    implementation libs.kotlin_coroutines_core


    testImplementation libs.junit
    testImplementation libs.truth
    testImplementation libs.kotlin_test
    testImplementation libs.mockito_core
}

javadoc {
  classpath += configurations.compileOnlyForJavadoc
}

task javadocZip(type: Zip) {
  dependsOn javadoc
  from javadoc.destinationDir
  archiveBaseName.set('javadoc')
}

group = 'com.android.tools.build.declarative'
version = rootProject.ext.buildVersion

base {
    archivesName.set("model")
}

project.ext.pomName = 'Declarative Gradle Model'
project.ext.pomDesc = 'APIs to load and parse declarative gradle build files.'

tasks.jar {
    // Set the version of the plugin in the manifest. This is used by the kotlin-android-extensions
    // plugin (and maybe other 3rd party plugins).
    manifest.attributes("Plugin-Version": version)
}

apply plugin: 'groovy'
apply plugin: 'clone-artifacts'
apply plugin: 'idea'

sourceSets {
    main {
        groovy.srcDirs = ['src/main/groovy', 'src/fromGradle/groovy']
        resources.srcDirs = ['src/main/resources', 'src/fromGradle/resources']
    }
    buildTest {
        groovy.srcDir file('src/build-test/groovy')
        groovy.srcDir file('src/integ-test/groovy')
        resources.srcDir file('src/build-test/resources')
        resources.srcDir file('src/integ-test/resources')
    }
    deviceTest {
        groovy.srcDir file('src/device-test/groovy')
        resources.srcDir file('src/device-test/resources')
    }
    integTest {
        groovy.srcDir file('src/integ-test/groovy')
        resources.srcDir file('src/integ-test/resources')
    }
}

ext.proguardVersion = "4.11"

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile project(':base:builder')
    compile project(':base:lint')
    compile "net.sf.proguard:proguard-gradle:${project.ext.proguardVersion}"

    testCompile 'junit:junit:3.8.1'

    buildTestCompile sourceSets.main.output
    buildTestCompile sourceSets.test.output
    buildTestCompile configurations.testCompile
    buildTestCompile configurations.testRuntime

    deviceTestCompile sourceSets.main.output
    deviceTestCompile sourceSets.test.output
    deviceTestCompile sourceSets.buildTest.output
    deviceTestCompile configurations.testCompile
    deviceTestCompile configurations.testRuntime

    integTestCompile sourceSets.main.output
    integTestCompile sourceSets.test.output
    integTestCompile configurations.testCompile
    integTestCompile configurations.testRuntime
}

// configuration for dependencies provided by the runtime,
// in this case proguard.
configurations{
    provided
}

dependencies{
    provided "net.sf.proguard:proguard-gradle:${project.ext.proguardVersion}"
}

//Include provided for compilation
sourceSets.main.compileClasspath += configurations.provided

idea {
    module {
        testSourceDirs += files('src/build-test/groovy', 'src/device-test/groovy').files

        scopes.COMPILE.plus += configurations.provided
    }
}

group = 'com.android.tools.build'
archivesBaseName = 'gradle'
version = rootProject.ext.buildVersion

project.ext.pomName = 'Gradle Plug-in for Android'
project.ext.pomDesc = 'Gradle plug-in to build Android applications.'

apply from: "$rootDir/buildSrc/base/publish.gradle"

jar.manifest.attributes("Plugin-Version": version)

task buildTest(type: Test) {
    testClassesDir = sourceSets.buildTest.output.classesDir
    classpath = sourceSets.buildTest.runtimeClasspath
    description = "Runs the project build tests. This requires an SDK either from the Android source tree, under out/..., or an env var ANDROID_HOME."
    group = "verification"
    systemProperties['jar.path'] = jar.archivePath

    useJUnit {
        excludeCategories "com.android.build.gradle.internal.test.category.DeviceTests"
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    description =
            "Runs the project integration tests. This requires an SDK either from the Android " +
                    "source tree, under out/..., or an env var ANDROID_HOME and a device."
    group = "verification"
    systemProperties['jar.path'] = jar.archivePath

    // Allow user to include/exclude categories from the command line.
    // e.g. add the following to skip device tests:
    // -DintegTest.excludeCategories=com.android.build.gradle.internal.test.category.DeviceTests
    useJUnit {
        if (System.properties[name + ".includeCategories"] != null) {
            includeCategories System.properties[name + ".includeCategories"]
        }
        if (System.properties[name + ".excludeCategories"] != null) {
            excludeCategories System.properties[name + ".excludeCategories"]
        }
    }
}

buildTest.dependsOn ':publishLocal'
check.dependsOn buildTest

integTest.dependsOn ':publishLocal'

task deviceTest(type: Test) {
    testClassesDir = sourceSets.deviceTest.output.classesDir
    classpath = sourceSets.deviceTest.runtimeClasspath
    description = "Runs the device tests. This requires a device."
    group = "verification"
    systemProperties['jar.path'] = jar.archivePath
}

deviceTest.dependsOn ':publishLocal'


groovydoc {
    exclude     "**/internal/**"
    includePrivate false

    docTitle "Gradle Plugin for Android"
    header ""
    footer "Copyright (C) 2012 The Android Open Source Project"
    overview ""
}

task javadocJar(type: Jar, dependsOn:groovydoc) {
    classifier  'javadoc'
    from        groovydoc.destinationDir
}

// add javadoc jar tasks as artifacts
artifacts {
    archives javadocJar
}

apply plugin: 'android'

dependencies {
    compile project(':lib')
}

android {
    compileSdkVersion 15
    buildToolsVersion "19.0.1"

    // This actual the app version code. Giving ourselves 100,000 values [0, 99999]
    defaultConfig.versionCode = 123

    flavorGroups "api", "abi"

    productFlavors {
        gingerbread {
            flavorGroup "api"
            minSdkVersion 10
            versionCode = 1
        }
        icecreamSandwich {
            flavorGroup "api"
            minSdkVersion 14
            versionCode = 2
        }
        x86 {
            flavorGroup "abi"
            ndk {
                abiFilter "x86"
            }
            // this is the flavor part of the version code.
            // It must be higher than the arm one for devices supporting
            // both, as x86 is preferred.
            versionCode = 3
        }
        arm {
            flavorGroup "abi"
            ndk {
                abiFilter "armeabi-v7a"
            }
            versionCode = 2
        }
        mips {
            flavorGroup "abi"
            ndk {
                abiFilter "mips"
            }
            versionCode = 1
        }
        fat {
            flavorGroup "abi"
            // fat binary, lowest version code to be
            // the last option
            versionCode = 0
        }
    }

    // make per-variant version code
    applicationVariants.all { variant ->
        // get the version code of each flavor
        def apiVersion = variant.productFlavors.get(0).versionCode
        def abiVersion = variant.productFlavors.get(1).versionCode

        // set the composite code
        variant.mergedFlavor.versionCode = apiVersion * 1000000 + abiVersion * 100000 + defaultConfig.versionCode
    }

}
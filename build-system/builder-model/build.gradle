apply plugin: 'com.android.tools.java-library'
apply plugin: 'com.android.tools.kotlin'
apply plugin: 'com.android.tools.publish'

configurations {
    versionPropertiesSource
}

dependencies {
    implementation libs.com.android.tools.annotations
    implementation libs.kotlin_stdlib
    versionPropertiesSource libs.com.android.tools.common
    testImplementation 'junit:junit:4.12'
    testImplementation libs.guava
    testImplementation libs.com.android.tools.testutils
}

group = 'com.android.tools.build'
version = rootProject.ext.buildVersion

base {
    archivesName.set("builder-model")
}

project.ext.pomName = 'Android Builder Model library'
project.ext.pomDesc = 'Model for the Builder library.'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task writeVersionProperties(type: com.android.tools.internal.VersionPropertiesWriter) {
    versionPropertiesSourceInput.from(configurations.versionPropertiesSource)
    outputFilePath = "com/android/builder/model/version.properties"
    outputDirectory = project.layout.buildDirectory.dir("generated/resources")
}

sourceSets {
    main {
        resources {
            srcDir(tasks.findByName("writeVersionProperties").getOutputDirectory())
        }
    }
}

def updateToolingModelApi = tasks.register('updateToolingModelApi', JavaExec) {
    classpath(sourceSets['test'].output.classesDirs, sourceSets['test'].runtimeClasspath)
    mainClass = 'com.android.builder.model.GradleToolingApiModelUpdater'
}

def updateApi = tasks.register('updateApi') {
    dependsOn(updateToolingModelApi)
}


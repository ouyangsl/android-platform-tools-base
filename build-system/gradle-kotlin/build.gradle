plugins {
    id 'com.android.tools.java-library'
    id 'com.android.tools.kotlin'
    id 'com.android.tools.publish'
    id 'java-gradle-plugin'
    id 'license-report'
}

gradlePlugin {
    plugins {
        comAndroidExperimentalKotlinSupport {
            id = "com.android.experimental.built-in-kotlin"
            implementationClass = "com.android.build.gradle.AndroidKotlinPlugin"
        }
        comAndroidKaptSupport {
            id = "com.android.legacy-kapt"
            implementationClass = "com.android.build.gradle.AndroidKaptPlugin"
        }
    }
}

validatePlugins {
    // Enable strict mode so we can see more warnings
    enableStricterValidation = true
}

// create an empty jar.
// Need to use a classifier to make it unique for publishing
// (since the name is specific to the artifactId)
project.tasks.register("apiJar", org.gradle.jvm.tasks.Jar) {
    from(file('NOTICE'))
    archiveClassifier.set("api")
}

// replace the api publication with the empty jar
configurations.apiElements.outgoing.artifacts.clear()
configurations.apiElements.outgoing.artifact(apiJar)

dependencies {
    implementation gradleApi()
    implementation libs.kotlin_stdlib
    implementation libs.kotlin_gradle_plugin
}

group = 'com.android.tools.build'
version = rootProject.ext.buildVersion

project.ext.pomName = 'Android Kotlin Gradle Plugin'
project.ext.pomDesc = 'Gradle plugin to register and configure Kotlin tasks for Android projects.'


plugins {
    id 'com.android.tools.java-library'
    id 'com.android.tools.kotlin'
    id 'java-gradle-plugin'
}

dependencies {
    implementation(project(':base:build-system:gradle-core'))

    implementation project(':base:build-system:gradle-settings-api')
    implementation libs.com.android.tools.analyticsLibrary.shared
    implementation libs.com.android.tools.common
    implementation libs.com.android.tools.repository

    compileOnly libs.kotlin_gradle_plugin
}

kotlin {
    sourceSets.configureEach {
        languageSettings.optIn("org.jetbrains.kotlin.gradle.ExternalKotlinTargetApi")
    }
}

gradlePlugin {
    plugins {
        comAndroidExperimentalKotlinMultiplatformLibrary {
            id = "com.android.experimental.kotlin.multiplatform.library"
            implementationClass =
                    "com.android.build.gradle.internal.plugins.KotlinMultiplatformAndroidPlugin"
        }
    }
}

group = 'com.android.tools.build'
version = rootProject.ext.buildVersion

apply plugin:'com.android.tools.publish'

publishing {
    publications {
        withType(MavenPublication) {
            artifactId = 'kmp-android-prototype'
        }
    }
}

project.ext.pomName = 'Gradle Plug-in for Android'
project.ext.pomDesc = 'Gradle plug-in to build Android applications.'
project.ext.customArtifactId = 'kmp-android-prototype'

tasks.register('zipKmpJar', Zip) {
    it.from(rootProject.ext.localRepo)
    it.destinationDirectory = rootProject.ext.androidHostDist
    it.archiveFileName = "kmp_prototype.zip"
    it.dependsOn(tasks.getByName("publish"))
}
